pkgname=rust-nightly-lrs
pkgver=2015.12.20
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='The Rust compiler and libraries.'
url='http://www.rust-lang.org/'
provides=('rust')
conflicts=('rust' 'rust-git' 'rust-nightly' 'rust-nightly-bin')
license=('MIT' 'Apache')
source=("https://static.rust-lang.org/dist/$(echo $pkgver | sed 's/\./-/g')/rustc-nightly-${CARCH}-unknown-linux-gnu.tar.gz"
        "https://static.rust-lang.org/dist/$(echo $pkgver | sed 's/\./-/g')/rust-std-nightly-aarch64-unknown-linux-gnu.tar.gz"
        "https://static.rust-lang.org/dist/$(echo $pkgver | sed 's/\./-/g')/rust-std-nightly-arm-unknown-linux-gnueabi.tar.gz"
        "https://static.rust-lang.org/dist/$(echo $pkgver | sed 's/\./-/g')/rust-std-nightly-i686-unknown-linux-gnu.tar.gz"
        "https://static.rust-lang.org/dist/$(echo $pkgver | sed 's/\./-/g')/rust-std-nightly-x86_64-unknown-linux-gnu.tar.gz"
        )
sha256sums=('b86747ed797bbaabfd288d2033955f727d04eb789315561b9f453bcf00704ede'
            'ef55c26ce772d8d796429f9a1b00f8debeeab15bd21d8dc15cc6853b29f6772a'
            'f59f578c0fe58bfa9eeebdbbff487645f3c06aae7c11ddc6bfe328d5319be7a2'
            'b88270a2c5a9e194a83fc258624c2874e0ef48af1ae50a3ade172fd34ec915a9'
            'ce5493654bfc0cb52fb9a997a7f7590ba1930930364a33feba10e65e3061701f')
options=(staticlibs !strip)

package() {
    local PREFIX=/usr

    cd rustc-nightly-${CARCH}-unknown-linux-gnu
    ./install.sh --prefix=${pkgdir}${PREFIX} --mandir=${pkgdir}${PREFIX}/man
    cd ..

    for arch in "aarch64-unknown-linux-gnu" \
                "arm-unknown-linux-gnueabi" \
                "i686-unknown-linux-gnu" \
                "x86_64-unknown-linux-gnu"; do
        cd rust-std-nightly-${arch}
        ./install.sh --prefix=${pkgdir}${PREFIX} --mandir=${pkgdir}${PREFIX}/man
        cd ..
    done

    rm -f ${pkgdir}${PREFIX}/lib/rustlib/manifest-*
    rm -f ${pkgdir}${PREFIX}/lib/rustlib/uninstall.sh
    rm -f ${pkgdir}${PREFIX}/lib/rustlib/install.log
}
